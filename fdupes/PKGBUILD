# Contributor: Edward E. <develinthedetail@gmail.com>

pkgname=fdupes
pkgver=1.6.1
pkgrel=1
pkgdesc="a program for identifying or deleting duplicate files residing within specified directories"
arch=('i686' 'x86_64')
license=('custom')
url="https://github.com/adrianlopezroche/fdupes"
#depends=('glibc')
#makedepends=('git')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/adrianlopezroche/fdupes/archive/v${pkgver}.tar.gz
        LICENSE)
sha256sums=('9d6b6fdb0b8419815b4df3bdfd0aebc135b8276c90bbbe78ebe6af0b88ba49ea'
            '6300e5a838d8a0f08b412742cb755c22708970aef420dbad5dc65ca7de677e39')
noextract=(${pkgname}-${pkgver}.tar.gz)

prepare() {
  [[ -d ${srcdir}/${pkgname}-${pkgver} ]] && rm -rf ${srcdir}/${pkgname}-${pkgver}
  bsdtar -xf ${srcdir}/${pkgname}-${pkgver}.tar.gz --exclude testdir
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make PREFIX=/usr
}

package(){
  cd ${srcdir}/${pkgname}-${pkgver}

  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm644 ${srcdir}/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
